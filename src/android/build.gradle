buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
    }
}

plugins {
  id "com.google.protobuf" version "0.9.4"
}

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def amazon = System.getenv('AMAZON')
def nordictrack = System.getenv('NORDICTRACK')
println(amazon)
println(nordictrack)

// Clean configurations to prevent conflicts
configurations {
    all {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    }
}

dependencies {
    implementation "androidx.core:core:1.12.0"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    
    // Single protobuf dependency - use only javalite
    implementation 'com.google.protobuf:protobuf-javalite:3.25.1'

    // Minimal gRPC dependencies with strict exclusions
    implementation('io.grpc:grpc-okhttp:1.63.0') {
        exclude group: 'com.google.protobuf'
        exclude group: 'io.grpc', module: 'grpc-protobuf'
    }
    implementation('io.grpc:grpc-protobuf-lite:1.63.0') {
        exclude group: 'com.google.protobuf'
    }
    implementation('io.grpc:grpc-stub:1.63.0') {
        exclude group: 'com.google.protobuf'
    }
    
    // Required for gRPC annotations
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    if(amazon == "1") {
        // amazon app store
        implementation('com.google.mlkit:text-recognition:16.0.0-beta6') {
            exclude group: 'com.google.protobuf'
        }
    } else if(nordictrack != "1") {
        // google play store
        implementation('com.google.android.gms:play-services-mlkit-text-recognition:18.0.2') {
            exclude group: 'com.google.protobuf'
        }
    }
    // NordicTrack build: no ML Kit dependency included

    implementation('com.google.android.play:core:1.8.2') {
        exclude group: 'com.google.protobuf'
    }

    def appcompat_version = "1.3.1"
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "com.android.billingclient:billing:6.0.1"
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation files('libs/usb-serial-for-android-3.8.1.aar')
    androidTestImplementation "com.android.support:support-annotations:28.0.0"
    implementation('com.google.android.gms:play-services-wearable:+') {
        exclude group: 'com.google.protobuf'
    }

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.60'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.60'
}

import org.apache.tools.ant.taskdefs.condition.Os

// Compatible with macOS on Apple Silicon
def archSuffix = Os.isFamily(Os.FAMILY_MAC) ? ':osx-x86_64' : ''

protobuf {
    protoc {
      artifact = "com.google.protobuf:protoc:3.25.1$archSuffix"
    }
    // Add gRPC plugin configuration
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.63.0"
        }
    }
    generateProtoTasks {
      all().configureEach { task ->
        // Add gRPC plugin to task
        task.plugins {
            grpc {
                option "lite"  // Essential for Android
            }
        }
        task.builtins {
          java {
            option "lite"
          }
        }
      }
    }
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    compileSdkVersion androidCompileSdkVersion.toInteger()

    buildToolsVersion '29.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst '**/META-INF/okio.kotlin_module'
        pickFirst '**/META-INF/kotlinx-coroutines-core.kotlin_module'
        pickFirst '**/META-INF/atomicfu.kotlin_module'
        pickFirst '**/META-INF/kotlinx-coroutines-io.kotlin_module'
        pickFirst '**/META-INF/kotlinx-io.kotlin_module'
        pickFirst '**/META-INF/kotlinx-coroutines-debug.kotlin_module'
        
        // Comprehensive protobuf conflict resolution
        pickFirst '**/com/google/protobuf/**'
        pickFirst '**/google/protobuf/**'
        pickFirst '**/*.proto'
        
        // Exclude conflicting metadata files
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/com.google.protobuf_protobuf-java.version'
        exclude 'META-INF/com.google.protobuf_protobuf-javalite.version'
        exclude 'META-INF/com.google.protobuf_protobuf-lite.version'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']            
       }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    defaultConfig {
        resConfig "en"
        compileSdkVersion 33
        minSdkVersion = 21
        targetSdkVersion = 34
        
        // Add multiDex support if needed due to large number of methods
        multiDexEnabled true
    }

tasks.all { task ->
     if (task.name == 'compileDebugJavaWithJavac' && amazon == "1") {
        task.dependsOn copyArm64Directory
        task.dependsOn copyArm32Directory
        }
}
}

// Strict resolution strategy to enforce single protobuf version
configurations.all {
    resolutionStrategy {
        // Force single protobuf version
        force 'com.google.protobuf:protobuf-javalite:3.25.1'
        
        // Fail on version conflict to catch issues early
        failOnVersionConflict()
        
        // Replace any protobuf-java with protobuf-javalite
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.google.protobuf') {
                if (details.requested.name == 'protobuf-java' || 
                    details.requested.name == 'protobuf-lite') {
                    details.useTarget group: 'com.google.protobuf', 
                                    name: 'protobuf-javalite', 
                                    version: '3.25.1'
                    details.because 'Enforce single protobuf version for Android'
                }
            }
        }
    }
}

task copyArm64Directory(type: Copy) {
    from "libs/arm64-v8a/"
    include '*arm64-v8a.so'
    into "libs/armeabi-v7a/"
}

task copyArm32Directory(type: Copy) {
    from "libs/armeabi-v7a/"
    include '*armeabi-v7a.so'
    into "libs/arm64-v8a/"
}